buildscript {
  repositories {
    mavenCentral()
    jcenter() // gradle-versions-plugin
    maven { url 'https://maven.fabric.io/public' }
  }

  dependencies {
    classpath 'me.tatarka:gradle-retrolambda:3.2.3'
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.11.3'
    classpath 'io.fabric.tools:gradle:1.21.2'
  }
}


apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'io.fabric'

def appId = "com.makingiants.today.todayhistory"
def appVersionName = "0.1.0"
def appVersionCode = 1

def gitSha() {
  'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
}

android {
  compileSdkVersion rootProject.sdkVersion
  buildToolsVersion rootProject.buildToolsVersion

  defaultConfig {
    applicationId appId
    minSdkVersion rootProject.minSdkVersion
    targetSdkVersion rootProject.sdkVersion
    versionCode appVersionCode
    versionName appVersionName

    // Add version and build number to apk name
    setProperty("archivesBaseName", "today-${appVersionName}-${appVersionCode}")

    buildConfigField "String", "HOST", "\"http://demo8732721.mockable.io\""
    buildConfigField "String", "GIT_SHA", "\"STATIC_FOR_PERFORMANCE\""
    buildConfigField "boolean", "ANALYTICS_ENABLED", "false"
  }
  buildTypes {
    debud {
      minifyEnabled true
      shrinkResources true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
    release {
      minifyEnabled true
      shrinkResources true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

      buildConfigField "String", "GIT_SHA", "\"${gitSha()}\""
      buildConfigField "boolean", "ANALYTICS_ENABLED", "true"
    }
  }

  lintOptions {
    fatal 'UnusedResources'
    abortOnError false
  }

  // Retrolambda
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}

repositories {
  maven { url 'https://maven.fabric.io/public' }
}

dependencies {
  compile rootProject.deps.appCompatV7
  compile "com.android.support:design:${rootProject.supportLibVersion}"

  // Management
  compile 'com.jakewharton.timber:timber:4.1.0@aar'
  compile('com.crashlytics.sdk.android:crashlytics:2.5.5@aar') { transitive = true; }

  compile project(':api')

  testCompile 'junit:junit:4.12'
}
