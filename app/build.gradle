buildscript {
  repositories {
    mavenCentral()
    jcenter() // gradle-versions-plugin
    maven { url 'https://maven.fabric.io/public' }
  }

  dependencies {
    classpath plugs.gradleVersions
    classpath plugs.kotlin
    classpath plugs.kotlinExtensions
    classpath 'io.fabric.tools:gradle:1.22.2'
  }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'io.fabric'
apply from: 'jacoco.gradle'

def appId = "com.makingiants.todayhistory"
def appVersionName = "0.1.0"
def appVersionCode = 1

def gitSha() {
  'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
}

android {
  compileSdkVersion setup.targetSdk
  buildToolsVersion setup.buildTools

  signingConfigs {
    debug {
      storeFile file("../debug.keystore")
    }
  }

  defaultConfig {
    applicationId appId
    minSdkVersion setup.minSdk
    targetSdkVersion setup.targetSdk
    versionCode appVersionCode
    versionName appVersionName

    // Add version and build number to apk name
    setProperty("archivesBaseName", "today-${appVersionName}-${appVersionCode}")

    buildConfigField "String", "HOST", "\"http://day-in-history.herokuapp.com\""
    buildConfigField "String", "GIT_SHA", "\"STATIC_FOR_PERFORMANCE\""
    buildConfigField "boolean", "ANALYTICS_ENABLED", "false"
  }
  buildTypes {
    debug {
      minifyEnabled false
      shrinkResources false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
    release {
      minifyEnabled true
      shrinkResources true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      signingConfig signingConfigs.debug

      buildConfigField "String", "GIT_SHA", "\"${gitSha()}\""
      buildConfigField "boolean", "ANALYTICS_ENABLED", "true"
    }
  }

  lintOptions {
    abortOnError false
  }

  // Kotlin
  sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
    test.java.srcDirs += 'src/test/kotlin'
  }
}

repositories {
  maven { url 'https://maven.fabric.io/public' }
}

// Dagger
kapt.generateStubs = true

dependencies {
  compile deps.supportAppCompat
  compile deps.supportDesign
  compile deps.supportCardView
  compile deps.supportPalette
  compile deps.kotlinStdLib
  compile project(':api')

  compile 'com.google.dagger:dagger:2.11'
  kapt 'com.google.dagger:dagger-compiler:2.11'
  provided 'org.glassfish:javax.annotation:10.0-b28'

  // Features
  compile 'com.squareup.picasso:picasso:2.5.2'
  compile 'com.github.florent37:picassopalette:1.0.2@aar'

  // Management
  compile 'com.jakewharton.timber:timber:4.5.1@aar'
  compile('com.crashlytics.sdk.android:crashlytics:2.6.8@aar') { transitive = true; }

  testCompile deps.kotlinTest
  testCompile 'junit:junit:4.12'
  testCompile "org.mockito:mockito-core:1.10.19"
  testCompile("com.nhaarman:mockito-kotlin:1.4.0") {
    exclude group: "org.jetbrains.kotlin", module: 'kotlin-stdlib'
    exclude group: "org.jetbrains.kotlin", module: 'kotlin-reflect'
  }
  testCompile('com.squareup.assertj:assertj-android:1.1.1') {
    exclude group: 'com.android.support', module: 'support-annotations'
  }
}